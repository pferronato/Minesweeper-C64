0 ;*** TO DO ***
1 ;* BUGS
2 ;MARKS TO BE COUNTED WHEN ZEROED
3 ;
4 ;* FEATURES
5 ;ADD SOUND
6 ;SPEED UP ARRAY INDEXING
7 ;ADD TIME AS INTERRUPT
8 ;ADD MOUSE CONTROL
9 ;SPEED UP ADDRSS * CALCULATION
10 REM ================================
20 REM           MINESC64
30 REM ================================
40 DEBUG
42 CLS:CLR:TBOMBS=16
43 TAG RESTART
45 RANDOM:TAG BLK=0,WHT=1,RED=2,LTGN=13,GRY=12,DGRY=11,BLU=6,LTGY=15
50 LET MAXSIDE=20:SUB ISIDE=MAXSIDE-1:LET CUR=102:DECIMAL TI,F0%,DD
60 DIM FIELD(399):DIM ZLX(399):DIM ZLY(399):DIM VIS(399)
61 VISIBLE=1:MARK=88:CL0=LTGR:LFT=400
70 BOMB=214:HIDDEN=230:EMPTY=255:SPACE=32:MARKED=0
75 ;TBOMBS=16
79 ;
80 SHOWMAP
85 INITFLD
87 GENBOMBS
90 MCALC
93 ;SHALL:KEYPRESS
95 INITCRS
110 MENU
120 RETURN
150 ;
160 PROC INITFLD
165 TAG SVIS=VIS():TAG SF=FIELD():FSIZ=400*2:EF=FSIZ+SF-1:EVIS=FSIZ+SVIS-1

174 FILL SF,EF,HIDDEN
177 FILL SVIS,EVIS,0
179 POKE 53280,0;SIDCLR:VOL 15:VOICE 1
270 RETURN
299 ;
300 PROC MOD:PASS N,D
320 DD=N/D
330 F0%=FRAC(DD)
340 RETURN
399 ;
400 PROC CLICK:LOCAL
420 ADSR 4,12,6,10
430 FREQ 20000
435 WAVE 1,1
440 FORI=0TO 1000
450 WAVE 0
460 GLOBAL:RETURN
580 ;
590 PROC COORD:PASS AX,BX
600 ;I=MAXSIDE*BX:ADD I=I+AX
605 ADD I=BX+BX:DUBL I:ADD I=I+BX:DUBL I:DUBL I:ADD I=I+AX
610 RETURN
710 ;
720 PROC CKCELL:PASS AA,BB:LET OOB=0
722 COMP ISIDE,AA:[BCC 738]
724 COMP ISIDE,BB:[BCC 738]
730 COMP AA,0:[BCC 738]
731 COMP BB,0:[BCC 738]
732 GOTO 740
738 LET<OOB=1
740 RETURN
770 ;
780 PROC DISP:LOCAL:PASS X,Y,P
782 ADD TY=Y+Y:DUBLTY:ADDTY=TY+Y:DUBLTY:DUBLTY:DUBLTY;Y0=40*Y+80
784 ADD Y0=TY+80
790 ADD XY=Y0+X:ADD XY=XY+1028;XY=Y0+X+1028
805 POKE XY,P
810 GLOBAL:RETURN
820 ;
830 PROC ZEROS:PASS A,B
850 ZLX(0)=A:ZLY(0)=B:LET ZLS=1:LET C=O:Y=0;X=0
860 TAG ZLOOP
870 A=ZLX(C):B=ZLY(C)
880 ADD X=A+1:ADD Y=B+1:ZEROED
890 ADD X=A+1:SUB Y=B-1:ZEROED
900 SUB X=A-1:ADD Y=B+1:ZEROED
910 SUB X=A-1:SUB Y=B-1:ZEROED
930 ADD X=A+1:LET<Y=B:ZEROED
940 LET<X=A:ADD Y=B+1:ZEROED
950 SUB X=A-1:LET<Y=B:ZEROED
960 LET<X=A:SUB Y=B-1:ZEROED
970 INC C:COMP C,ZLS:[BCC ZLOOP]
977 RETURN
979 ;
980 PROC ZEROED
990 CKCELL.X,Y:COORD.X,Y
995 IF VIS(I)=VISIBLE THEN RETURN
1000 COMP OOB,1:[BEQ 1030]
1010 IF FIELD(I)=0 THEN ZLX(ZLS)=X:ZLY(ZLS)=Y
1020 THEN INC ZLS:VIS(I)=VISIBLE
1025 THEN DISP.X,Y,SPACE:DEC LFT:RETURN
1027 DEC LFT:P=FIELD(I)+48:DISP.X,Y,P:VIS(I)=VISIBLE
1030 RETURN
1060 ;
1070 PROC EXIT
1080 POKE 646,LTGY:CLS:LOC 0,0;"MINES 64 - BYE BYE":POKE 646,LTGN:END
1090 ;
1100 PROC PROG:PASS C,M
1105 LET 646=WHT
1110 LOC 6,23;MSG$;" ";C;" /";M;"  "
1120 COMP C,M:[BNE 1130]
1125 LOC 6,23 "                   "
1130 LET 646=LTGN:RETURN
1139 ;
1200 PROC MENU
1203 SCORES
1205 LET SX=25:P0=40*20+10+1024
1210 CHR$(151):POKE 646,WHT
1220 LOC SX,08;"[RET] OPEN"
1230 LOC SX,10;"[SPC] MARK"
1240 LOC SX,12;"[R]   RESTART"
1250 LOC SX,14;"[B]   BOMBS"
1255 LOC SX,16;"USE ARROWS KEYS";CHR$(153)
1260 GETK$:IF K$="" THEN 1260
1290 IF K$="R" THEN MREST
1291 IF K$="B" THEN MCONF
1293 IF K$="Q" THEN MEXIT
1294 CURS:CKEND:SCORES
1295 GOTO 1260
1299 ;
1800 TAG FAILED
1805 SCORES:SVSCR
1810 MSG$="BOOOM!":WIND
1830 KEYPRESS
1835 RSTSCR
1838 POKE 646,LTGN:RESTART
1908 ;
1910 PROC SCORES
1915 POKE 646,LTGY
1930 LOC 4,0;"MARKED:    LEFT:     BOMBS:   "
1940 LOC11,0;MARKED:LOC20,0;LFT;LOC31,0;TBOMBS
1950 POKE646,LTGN:RETURN
1999 ;
2000 PROC SHOWMAP:LET L=19:LET X=2:LET Y=4:POKE 646,BLU
2001 LOC3,1;".";DUP$(".",20);"."
2002 LOC3,22;".";DUP$(".",20);"."
2003 FORI=2TO21:LOC3,I;".":NEXT
2004 FORI=2TO21:LOC24,I;".":NEXT:POKE 646,LTGR
2010 FORI=0TOISIDE
2020 ADD X1=X+I
2030 ADD S=X1+X1:DUBL S:ADD S=S+X1
2040 DUBL S:DUBL S:DUBL S:SS=S+1024+Y
2045 COLS=S+55296+Y
2060 ADD EE=SS+L:ADD CLE=COLS+L
2062 FILLCOLS,CLE,LTGN
2070 FILL SS,EE,HIDDEN
2080 NEXT
2090 RETURN
2099 ;
2100 PROC MREST
2110 CONFIRM
2130 IFM$="Y" THEN RESTART
2150 RETURN
2155 ;
2160 PROC CONFIRM
2170 SVSCR
2172 MSG$= "SURE? (Y/N)":WIND
2175 GETM$:IFM$=""THEN2175
2177 RSTSCR
2185 POKE 646,LTGN
2187 RETURN
2199 ;
2200 PROC MEXIT
2210 CONFIRM
2230 IFM$="Y" THEN EXIT
2250 RETURN
2259 ;
2300 PROC CKEND
2310 IF MARKED=TBOMBS AND LFT=MARKED THEN WIN
2315 ELSE RETURN
2320 TAG WIN
2330 SVSCR:MSG$="YOU WON":WIND
2340 KEYPRESS
2345 RSTSCR:RESTART
2350 RETURN
2499 ;
2500 PROC CURS
2505 LET MV=0;CLICK
2510 IF K$="" THEN DEC R%:INC MV
2520 IF K$="" then inc r%:inc mv
2530 if k$="." then inc c%:inc mv
2540 if k$="." then dec c%:inc mv
2543 r$=chr$(13):if k$=r$ then ocell
2545 s$=chr$(32):if k$=s$ then mcell
2546 comp mv,0:[beq 2620]
2550 if r%=65535 then let r%=19
2560 if c%=65535 then let c%=19
2570 if r%>19 then let r%=0
2580 if c%>19 then let c%=0
2585 add r1=r%+2:add c1=c%+4
2590 l=40*r1+c1+1024:pc=40*r1+c1+55296
2600 poke p0,st:st=peek(l):poke pc0,cl0
2610 poke l,cur:let p0=l:cl0=peek(pc):poke pc,0:let pc0=pc
2620 return
2699 ;
2700 proc initcrs:c%=0:r%=0
2710 c1=4:r1=2:pc=40*r1+c1+55296:l=40*r1+c1+1024
2715 poke l,hidden:poke pc,blk:cl0=ltgn:st=hidden:p0=l:pc0=pc
2720 return
2799 ;
2800 proc ocell
2805 coord.c%,r%
2807 if vis(i)=visible then return
2808 if vis(i)=mark then return
2810 if field(i)=0 then zeros.c%,r%:let p0=space:goto 2840
2820 if field(i)=bomb then failed
2825 if vis(i)=mark then return
2830 px=field(i)+48:disp.c%,r%,px:let st=px:dec lft
2840 return
2999 ;
3000 proc mcell
3010 coord.c%,r%:if vis(i)=visible then return
3015 if vis(i)=mark then dec marked:let st=hidden:vis(i)=0:let cl0=ltgn:re
turn
3020 disp.c%,r%,mark:inc marked:let st=mark:vis(i)=mark:let cl0=red
3998 return
3999 ;
4000 proc genbombs
4010 decimal v:msg$="bombs":let p=399
4020 fori=0to399:vis(i)=i:next
4025 fori=1totbombs
4030 v=rnd*p:r=int(v):w=vis(r)
4035 field(w)=bomb:vis(r)=vis(p):vis(p)=w:dec p
4060 next
4065 fill svis,evis,0
4070 return
4499 ;
4500 proc shall
4510 for r=0to19
4520 for c=0to19
4530 coord.c,r:if field(i)=bomb then disp.c,r,bomb:goto 4540
4538 p=field(i)+48:disp.c,r,p
4540 next
4543 prog.r,19
4545 next
4550 return
4999 ;
5000 proc mcalc
5010 let<nb=0:msg$="init"
5020 fori=0to399
5025 fi=field(i)
5030 comp fi,bomb:[beq 5130]
5040 add s=i+01:comps,400:[bcs 5050]
5045 if field(s)=bomb then inc nb
5050 sub s=i-01:comps,0:[bcc 5060]
5055 if field(s)=bomb then inc nb
5060 add s=i+20:comps,400:[bcs 5070]
5065 if field(s)=bomb then inc nb
5070 sub s=i-20:comps,0:[bcc 0]
5075 if field(s)=bomb then  inc nb
5080 add s=i+21:comps,400:[bcs 5090]
5085 if field(s)=bomb then inc nb
5090 add s=i+19:comps,400:[bcs 5100]
5095 if field(s)=bomb then inc nb
5100 sub s=i-19:comps,0:[bcc 5105]
5105 if field(s)=bomb then inc nb
5110 sub s=i-21:comps,0:[bcc 5120]
5115 if field(s)=bomb then inc nb
5120 field(i)=nb:let nb=0:prog.i,399
5130 next
5140 return
5999 ;
6000 proc wind
6005 ln=len(msg$):sub ln=17-ln:half ln
6020 poke646,wht:loc11,11;"WDDDDDDDDDDDDDDDW"
6030 loc11,12;"B";dup$(" ",15);"B"
6040 poke646,wht:loc11,13;"WDDDDDDDDDDDDDDDW"
6045 poke646,red:loc11,12;spc(ln);msg$
6050 return
6199 ;
6200 proc svscr
6210 copy 1024,2024,40960
6215 copy 55296,56295,41961
6220 return
6249 ;
6250 proc rstscr
6260 copy 40960,41960,1024
6265 copy 41961,42961,55296
6270 return
6399 ;
6400 proc mconf
6410 svscr
6420 loc 12,12;"bombs[";tbombs;"]:"
6430 loc 22,12;:input k$
6440 if k$="" then return
6445 vk=val(k$)
6450 if vk<1 then 6430
6452 if vk>50  then 6430
6455 tbombs=vk
6460 rstscr
6470 restart
6480 return

